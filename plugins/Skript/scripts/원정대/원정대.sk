variables:
	# (0-아무상태도 아님 1-파티초대받은상태 2-원정대원인상태 3-파티장인상태)
	{Expedition::session::%player%} = 0
	{Expedition::PartyBoss::%player%} = 0
options:
	Prefix:&6&l[원정대]&8&l-
	message-color:&o&7

on load:
	send "-------------------------------------------------------- " to console
	send " _    _           _        __          __        _     _ " to console
	send "| |  | |         | |       \ \        / /       | |   | |" to console
	send "| |  | |_ __   __| | ___ _ _\ \  /\  / /__  _ __| | __| |" to console
	send "| |  | | '_ \ / _` |/ _ \ '__\ \/  \/ / _ \| '__| |/ _` |" to console
	send "| |__| | | | | (_| |  __/ |   \  /\  / (_) | |  | | (_| |" to console
	send " \____/|_| |_|\__,_|\___|_|    \/  \/ \___/|_|  |_|\__,_|" to console
	send "@Moonlight_flower	+	@Moonlight_kotean	   " to console
	send "Copyrightⓒ2019 Moonlight_Flower Moonlight_Kotean All rights reserved." to console
	send "-------------------------------------------------------- " to console
	set {world::vur} to "&c&l[UW]&r &6언더월드 프로젝트 - 원정대 ( v1.0 )"
	send "%{world::vur}% 로드 성공" to console

on unload:
	delete {world::vur}

command /원정대 [<text>] [<offline player>]:
	trigger:
		if arg 1 is not set:
			send "+---------------------------------------+"
			send "{@Prefix}{@message-color}/원정대 초대 <플레이어> -> 플레이어를 원정대에 초대합니다 속한 원정대가 없다면 생성후 초대합니다."
			send "{@Prefix}{@message-color}/원정대 수락"
			send "{@Prefix}{@message-color}/원정대 추방 <플레이어>"
			send "{@Prefix}{@message-color}/원정대 탈퇴"
			send "{@Prefix}{@message-color}/원정대 정보"
			send "+---------------------------------------+"
		if arg 1 is "초대":
			# 초대전 검증
			# 파티 생성자 상태 체크 (0-생성가능 1-파티초대받은상태 2-원정대원인상태 3-파티장인상태)
			if {Expedition::session::%player%} is 1:
				send "{@Prefix}{@message-color}당신은 다른원정대에 초대받은 상태입니다.."
				stop
			if {Expedition::session::%player%} is 2:
				send "{@Prefix}{@message-color}이미 원정대에 속한상태입니다 원정대 초대는 원정대장만 가능합니다."
				stop

			# 유저명이 입력이 되었는지 체크
			if arg 2 is not set:
				send "{@Prefix}{@message-color}초대할 플레이어명을 입력해 주세요. /원정대 초대 <플레이어>"
				stop

			# 유저가 온라인인지 체크
			if arg 2 is not online:
				send "{@Prefix}{@message-color}해당플레이어가 접속중이 아닙니다.."
				stop

			# 초대할 유저의 UUID 서버와 동기화유무 체크
			if {uuid::%arg 2%} is not set:
				send "{@Prefix}{@message-color}UUID 동기화 에러! 해당플레이어는 서버와 계정정보가 연동되지 않은상태입니다. 초대할 수 없습니다."
				stop

			# 초대할 플레이어 상태 체크 (0-초대가능 1-파티초대받은상태 2-원정대에 속한상태 3-다른원정대의 원정대장)
			if {Expedition::session::%arg 2%} is 1:
				send "{@Prefix}{@message-color}해당 플레이어는 이미 받은 초대가 있습니다."
				stop
			if {Expedition::session::%arg 2%} is 2:
				send "{@Prefix}{@message-color}해당 플레이어는 이미 원정대에 속한 상태입니다."
				stop
			if {Expedition::session::%arg 2%} is 3:
				send "{@Prefix}{@message-color}해당 플레이어는 이미 다른 원정대의 대장입니다."
				stop

			# 자기 자신인지 체크
			if player is arg 2:
				send "{@Prefix}{@message-color}자기 자신은 초대할 수 없습니다."
				stop
			# 초대시작 **********
			# 초대받은 플레이어의 상태 변경 (0-초대가능 1-파티초대받은상태 2-원정대에 속한상태 3-다른원정대의 원정대장)
			set {Expedition::session::%arg 2%} to 1
			# 초대한 사용자의 정보 전송
			set {Expedition::PartyBoss::%arg 2%} to ("%player%" parsed as offline player)
			send "{@Prefix}{@message-color}%player%님이 원정대 가입을 요청하였습니다 10초안에 응답하지 않으면 자동취소 됩니다." to arg 2
			send "{@Prefix}{@message-color}%arg 2%님에게 원정대 가입을 요청하였습니다 플레이어가 10초안에 응답하지 않으면 자동취소 됩니다." to player
			send arg 2 title "%player%의 &6원정대 초대" with subtitle "10초 안에 '/원정대 수락' 을 입력하지 않으면 초대가 거절됩니다'" for 5 seconds
			# 10초간 대기
			wait 10 seconds
			# 초대받은 플레이어가 수락하지않고 대기할경우 (0-초대가능 1-파티초대받은상태 2-원정대에 속한상태 3-다른원정대의 원정대장)
			#초대받은 플레이어의 상태 초기화
			if {Expedition::session::%arg 2%} is 1:
				set {Expedition::session::%arg 2%} to 0
				set {Expedition::PartyBoss::%arg 2%} to 0
				send "{@Prefix}{@message-color}%arg 2%님이 원정대 초대에 응답하지 않았습니다." to player
				send "{@Prefix}{@message-color}%player%님의 원정대 초대를 거절하였습니다.." to arg 2
		# =========================================================================================================
		# =========================================================================================================
		# =========================================================================================================
		# =========================================================================================================
		if arg 1 is "수락":
			# 플레이어의 상태 조회 (0-생성가능 1-파티초대받은상태 2-원정대원인상태 3-파티장인상태)
			if {Expedition::session::%player%} is 0:
				send "{@Prefix}{@message-color}원정대초대를 받은 상태가 아닙니다.."
				stop
			if {Expedition::session::%player%} is 2:
				send "{@Prefix}{@message-color}이미 원정대에 속한 상태입니다."
				stop
			if {Expedition::session::%player%} is 3:
				send "{@Prefix}{@message-color}이미 다른 원정대의 대장입니다"
				stop
			# 파티 자리 유무 체크
			set {_check} to 0
			loop {Expedition::Partymember::%{Expedition::PartyBoss::%player%}%::*}:
				add 1 to {_check}
			if {_check} > 3:
				send "{@Prefix}{@message-color}원정대에 자리가 없습니다.."
				stop
			# 파티보스 이름으로 된 파티멤버 리스트에 닉네임 등록
			add ("%player%" parsed as offline player) to {Expedition::Partymember::%{Expedition::PartyBoss::%player%}%::*}
			# 플레이어 직위 변경
			set {Expedition::session::%player%} to 2
			# 초대한 플레이어 직위 변경
			set {Expedition::session::%{Expedition::PartyBoss::%player%}%} to 3
			#안내메시지
			send "{@Prefix}{@message-color}%{Expedition::PartyBoss::%player%}%님의 원정대에 가입하였습니다."
			send "{@Prefix}{@message-color}%player%님이 원정대에 가입하였습니다." to {Expedition::PartyBoss::%player%}
			#파티에 속한 모든 플레이어에게 안내 메시지
			loop {Expedition::Partymember::%{Expedition::PartyBoss::%player%}%::*}:
				send "{@Prefix}{@message-color}%loop-value% 님이 %{Expedition::PartyBoss::%player%}%님의 원정대에 가입하였습니다." to loop-value
				
		if arg 1 is "추방":
			# 플레이어의 상태가 파티장인지 조회 (0-생성가능 1-파티초대받은상태 2-원정대원인상태 3-파티장인상태)
			if arg 2 is not set:
				send "{@Prefix}{@message-color}추방할 플레이어명을 입력해 주세요. /원정대 추방 <플레이어>"
				stop
			if player is arg 2:
				send "{@Prefix}{@message-color}자기 자신은 추방할 수 없습니다."
				stop
			if {Expedition::session::%player%} is 3:
				# 파티인원 유무 조회
				loop {Expedition::Partymember::%player%::*}:
					if loop-value is arg 2:
						set {_found} to 1
						exit loop
					else:
						set {_found} to 0
				if {_found} is 0:
					send "{@Prefix}{@message-color}가입된 원정대원이 아닙니다."
					stop
				if {_found} is 1:
					send "{@Prefix}{@message-color}%arg 2% 님을 추방하였습니다."
					# 전체 안내메시지
					loop {Expedition::Partymember::%player%::*}:
						send "{@Prefix}{@message-color}%arg 2% 님이 원정대에서 추방되었습니다." to loop-value
					# 파티리스트에서 삭제
					remove "%arg 2%" from {Expedition::Partymember::%player%::*}
					# 추방된 플레이어 직위 변경
					set {Expedition::session::%arg 2%} to 0
					set {Expedition::PartyBoss::%arg 2%} to 0
			else:
				send "{@Prefix}{@message-color}원정대장이 아닙니다.."

		if arg 1 is "탈퇴":
			# 플레이어의 상태 조회 (0-생성가능 1-파티초대받은상태 2-원정대원인상태 3-파티장인상태)
			if {Expedition::session::%player%} is 0:
				send "{@Prefix}{@message-color}가입된 원정대가 없습니다."
			if {Expedition::session::%player%} is 1:
				send "{@Prefix}{@message-color}가입된 원정대가 없습니다."
			if {Expedition::session::%player%} is 2:
				# 파티장 조회
				send "{@Prefix}{@message-color}%{Expedition::PartyBoss::%player%}%님의 원정대에서 탈퇴합니다.."
				# 해당 파티장의 리스트에서 유저 삭제
				remove "%player%" from {Expedition::Partymember::%{Expedition::PartyBoss::%player%}%::*}
				# 전체 안내메시지
				loop {Expedition::Partymember::%{Expedition::PartyBoss::%player%}%::*}:
					send "{@Prefix}{@message-color}%player% 님이 탈퇴하였습니다." to loop-value
				send "{@Prefix}{@message-color}%player% 님이 탈퇴하였습니다." to {Expedition::PartyBoss::%player%}
				# 플레이어 직위 변경
				set {Expedition::session::%player%} to 0
				set {Expedition::PartyBoss::%player%} to 0
			if {Expedition::session::%player%} is 3:
				# 파티에 속한 모든 파티원들 강제 파티 탈퇴진행 안내
				loop {Expedition::Partymember::%player%::*}:
					send "{@Prefix}{@message-color}원정대장이 파티를 해산하였습니다." to loop-value
					set {Expedition::session::%loop-value%} to 0
					set {Expedition::PartyBoss::%loop-value%} to 0
				set {Expedition::session::%player%} to 0
				set {Expedition::PartyBoss::%player%} to 0
				clear {Expedition::Partymember::%player%::*}
				send "{@Prefix}{@message-color}파티를 해산하였습니다."
				# 파티원 파티탈퇴 처리후 상태 초기화

			
		if arg 1 is "정보":
			send "+---------------------------------------+"
			send "{@Prefix}{@message-color}개발자용 정보 출력"
			send "{@Prefix}{@message-color}상태 : %{Expedition::session::%player%}% (0-가입안됨 1-파티초대받음 2-원정대원 3-원정대장)"
			if {Expedition::session::%player%} is 2:
				send "{@Prefix}{@message-color}원정대장 : %{Expedition::PartyBoss::%player%}%"
				loop {Expedition::Partymember::%{Expedition::PartyBoss::%player%}%::*}:
					send "원정대멤버 :%loop-index% %loop-value% "
			if {Expedition::session::%player%} is 3:
				send "{@Prefix}{@message-color}원정대장 : %player%"
				loop {Expedition::Partymember::%player%::*}:
					send "당신이 구성한 원정대 멤버 :%loop-index% %loop-value% "
			send "+---------------------------------------+"
